buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath 'org.lsposed.lsparanoid:gradle-plugin:0.5.2'
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.19.0'
    id 'com.dorongold.task-tree' version '2.1.1'
}

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xlint:all,-processing'
    }

    project.ext {
        compileSdkVer = 33
        buildToolsVer = '34.0.0'
        minSdkVer = 24
        targetSdkVer = 33

        String ndkVer = '25.1.8937393'

        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        String sdkDir = properties.getProperty('sdk.dir')
        ndkDir = new File(sdkDir, 'ndk/' + ndkVer).getAbsolutePath()

        daemonDex = 'daemon.dx'
        nativeDir = rootDir.absolutePath + '/native'

        noPro = !new File(rootDir, 'configs/pro-app.gradle').isFile()
    }
}

// https://github.com/diffplug/spotless/issues/1380#issuecomment-1405392506
spotless {
    predeclareDeps()
}
spotlessPredeclare {
    java {
        googleJavaFormat()
    }
}
